# Use default configuration values. Override with Snakemake's --configfile/--config options.
configfile: "defaults/config.yaml"


rule all:
    input:
        "results/sequences.fasta",
        "results/metadata.tsv",


include: "rules/fetch_from_ncbi.smk"
include: "rules/curate.smk"

# If included, the nextclade rules will create the final metadata TSV
# by joining the Nextclade output with the metadata. However, if not
# including nextclade, we have to rename the subset metadata TSV to
# the final metadata TSV.
if "nextclade" in config:

    include: "rules/nextclade.smk"

else:

    rule create_final_metadata:
        input:
            metadata="data/subset_metadata.tsv",
        output:
            metadata="results/metadata.tsv",
        shell:
            """
            mv {input.metadata:q} {output.metadata:q}
            """



rule clean:
    params:
        targets = [
            "benchmarks",
            "data",
            "logs",
            "results",
            ]
    shell:
        """
        rm -rfv {params.targets}
        """
